plugins {
  id 'org.jetbrains.dokka' version '0.10.1'
  id 'org.jetbrains.kotlin.jvm' version '1.3.72'
  id 'org.jlleitschuh.gradle.ktlint' version '9.3.0'
}

dependencies {
  api project(':dbsetup-spreadsheet')
  api 'com.ninja-squad:DbSetup-kotlin:2.1.0'

  testRuntimeOnly 'com.h2database:h2:1.4.200'
  testRuntimeOnly 'org.apache.poi:poi-ooxml:4.1.2'
  testImplementation 'org.assertj:assertj-db:2.0.0'
  testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
  kotlinOptions {
    apiVersion = '1.0'
    jvmTarget = '1.8'
  }
}

test {
  useJUnitPlatform()
}

jar {
  manifest {
    attributes(
      'Export-Package': 'com.sciencesakura.dbsetup.spreadsheet',
      'Import-Package': 'com.ninja_squad.dbsetup_kotlin,'
        + 'com.sciencesakura.dbsetup.spreadsheet'
        + ";bundle-symbolic-name=\"${project.group}.${project(':dbsetup-spreadsheet').name}\""
        + ";bundle-version=\"${project.version}\""
    )
  }
}

dokka {
  outputDirectory = file("$rootDir/docs/kotlin")
  configuration {
    jdkVersion = 8
  }
}

clean {
  delete dokka.outputDirectory
}

assemble.dependsOn(kotlinSourcesJar)
