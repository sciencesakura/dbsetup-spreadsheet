plugins {
  id 'org.jetbrains.dokka' version '1.4.30'
  id 'org.jetbrains.kotlin.jvm' version '1.5.31'
  id 'org.jlleitschuh.gradle.ktlint' version '10.2.0'
}

dependencies {
  api project(':dbsetup-spreadsheet')
  api 'com.ninja-squad:DbSetup-kotlin:2.1.0'

  testRuntimeOnly 'com.h2database:h2:1.4.200'
  testRuntimeOnly 'org.apache.logging.log4j:log4j-core:2.14.1'
  testRuntimeOnly 'org.apache.poi:poi-ooxml:5.1.0'
  testImplementation 'org.assertj:assertj-db:2.0.2'
  testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
}

test {
  useJUnitPlatform()
}

jar {
  manifest {
    attributes(
      'Export-Package': 'com.sciencesakura.dbsetup.spreadsheet',
      'Import-Package': 'com.ninja_squad.dbsetup_kotlin,'
        + 'com.sciencesakura.dbsetup.spreadsheet'
        + ";bundle-symbolic-name=\"${project.group}.${project(':dbsetup-spreadsheet').name}\""
        + ";bundle-version=\"${project.version}\""
    )
  }
}

dokkaHtml {
  outputDirectory.set(file("$rootDir/docs/kotlin"))
}

dokkaJavadoc {
  outputDirectory.set(temporaryDir)
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
  archiveClassifier.set('javadoc')
  from dokkaJavadoc.outputDirectory
}

publishing {
  publications {
    maven(MavenPublication) {
      artifact kotlinSourcesJar
      artifact javadocJar
    }
  }
}
